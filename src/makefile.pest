// Documentation: https://pest.rs/book/grammars/grammars.html

link = { ("http://" | "https://" | "ssh://") ~ (LETTER | ":" | "." | "-" | "/" | NUMBER)+ }
text = { (link | LETTER | SPACE_SEPARATOR | PUNCTUATION | NUMBER | SYMBOL)+ }
color = { "#" ~ (LETTER | NUMBER)+ }

name = { "#@name " ~(text)* }
description = { "#@description " ~(text)* }
color_title = { "#@color-title " ~(color)* }
color_subtitle = { "#@color-subtitle " ~(color)* }
color_link = { "#@color-link " ~(color)* }

help = _{ "## " ~ (text)* }

phony = _{ ".PHONY: " ~ (!(NEWLINE) ~ ANY)+ }

target_name = { (!(":") ~ ANY)+ }

target_with_help = {
  (help ~ NEWLINE)+
  ~ (phony ~ NEWLINE)*
  ~ target_name
}

help_section_sep = { "# --" ~ ("-")* }

not_a_section_sep = { !("-") ~ (ANY) }

help_section_title = { (!(NEWLINE) ~ ANY)+ }

help_section_block = _{ "# " ~ help_section_title }

help_section = {
  help_section_block
  ~ NEWLINE
  ~ help_section_sep
}

makefile = _{ SOI ~ (name | description | color_title | color_subtitle | color_link | target_with_help | help_section | ANY)* ~ EOI}
