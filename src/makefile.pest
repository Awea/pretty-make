// Documentation: https://pest.rs/book/grammars/grammars.html

link = { (!(":" | "/") ~ LETTER | NUMBER)+ ~ "://" ~ (!(NEWLINE | SPACE_SEPARATOR) ~ ANY)+ }
text = { (link | LETTER | SPACE_SEPARATOR | PUNCTUATION | NUMBER | SYMBOL)+ }
color = { "#" ~ (LETTER | NUMBER)+ }

name = { "#@name " ~(text)* }
description = { "#@description " ~(text)* }
color_title = { "#@color-title " ~(color)* }
color_subtitle = { "#@color-subtitle " ~(color)* }
color_link = { "#@color-link " ~(color)* }

help = _{ "## " ~ (text)* }

comment = _{ "#" ~ !("#") ~ (!(NEWLINE) ~ ANY)* }

phony = _{ ".PHONY: " ~ (!(NEWLINE) ~ ANY)+ }

target_name = { (!(":" | NEWLINE) ~ ANY)+ }

other_line = _{ !(help | phony | target_name ~ ":") ~ (!(NEWLINE) ~ ANY)+ }

target_with_help = {
  (help ~ NEWLINE)+
  ~ (comment ~ NEWLINE | phony ~ NEWLINE | other_line ~ NEWLINE)*
  ~ target_name
}

help_section_sep = { "# --" ~ ("-")* }

not_a_section_sep = { !("-") ~ (ANY) }

help_section_title = { (!(NEWLINE) ~ ANY)+ }

help_section_block = _{ "# " ~ help_section_title }

help_section = {
  help_section_block
  ~ NEWLINE
  ~ help_section_sep
}

makefile = _{ SOI ~ (name | description | color_title | color_subtitle | color_link | target_with_help | help_section | comment | ANY)* ~ EOI}
